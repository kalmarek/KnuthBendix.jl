## KBS using index automata for rewriting

"""
    KBIndex <: KBS2Alg <: CompletionAlgorithm
The Knuth-Bendix completion algorithm that (if successful) yields the reduced,
confluent rewriting system generated by rules of `rws`.

`KBIndex` uses index automaton to perform rewrites, and delays
the incorporation of newly found rules by maintaining a stack of
them of size controlled by `stack_size` of the `Settings` passed along to
`knuthbendix` call.

Additionally confluence checks by backtrack search on the index automaton
are performed after periods when no new rules were discovered.
"""
struct KBIndex <: KBS2Alg end

Settings(alg::KBIndex) = Settings(alg; max_rules = 10_000, stack_size = 100)

function time_to_rebuild(settings::Settings, ::AbstractRewritingSystem, stack)
    ss = settings.stack_size
    return ss <= 0 || length(stack) > ss
end

function remove_inactive!(rws::RewritingSystem, i::Integer, j::Integer)
    # compute the shifts for iteration indices
    lte_i = 0 # less than or equal to i
    lte_j = 0
    for (idx, r) in pairs(__rawrules(rws))
        if !isactive(r)
            if idx ≤ i
                lte_i += 1
            end
            if idx ≤ j
                lte_j += 1
            end
        end
        idx ≥ max(i, j) && break
    end
    i -= lte_i
    j -= lte_j
    i = max(i, firstindex(__rawrules(rws)))
    j = max(j, firstindex(__rawrules(rws)))

    remove_inactive!(rws)
    return i, j
end

function knuthbendix!(
    settings::Settings{KBIndex},
    rws::AbstractRewritingSystem,
)
    if !isreduced(rws)
        rws = reduce!(settings.algorithm, rws)
    end
    # rws is reduced now so we can create its index
    idxA = IndexAutomaton(rws)
    work = Workspace(idxA, settings)
    knuthbendix!(work, rws, idxA)

    __post!(rws, idxA, work)
    return rws
end

function knuthbendix!(
    work::Workspace{KBIndex},
    rws::AbstractRewritingSystem{W},
    idxA::IndexAutomaton = IndexAutomaton(rws),
) where {W}
    @assert isreduced(rws)
    stack = Vector{Tuple{W,W}}()
    rwrules = __rawrules(rws)
    settings = work.settings

    i = firstindex(rwrules)
    while i ≤ lastindex(rwrules)
        if time_to_check_confluence(rws, work)
            if settings.verbosity == 2
                @info "no new rules found for $(settings.confluence_delay) itrs, attempting a confluence check at" i,
                rwrules[i]
            end
            if !isempty(stack)
                rws, (i, _) =
                    reduce!(settings.algorithm, rws, stack, i, 0, work)
                idxA = Automata.rebuild!(idxA, rws)
            end
            @assert isempty(stack)
            stack, i_after = check_confluence!(stack, rws, idxA, work)
            if isempty(stack)
                __post!(rws, idxA, work)
                return rws # yey, we're done!
            end
            if settings.verbosity == 2
                l = length(stack)
                @info "confluence check failed: found $(l) new rule$(l==1 ? "" : "s")"
            end
            # @info (i, i_after)
            i = max(i, i_after)
            work.confluence_timer = 0
        end

        work.confluence_timer += 1
        ri = rwrules[i]
        j = firstindex(rwrules)
        while j ≤ i
            if are_we_stopping(settings, rws)
                return reduce!(settings.algorithm, rws, work)
            end

            # TODO: can we multithread this part?
            # Note:
            #   1. each thread needs its own stack, work;
            #   2. idxA stores path which makes rewriting with it thread unsafe

            rj = rwrules[j]
            l = length(stack)
            stack = find_critical_pairs!(stack, idxA, ri, rj, work)
            if ri !== rj
                stack = find_critical_pairs!(stack, idxA, rj, ri, work)
            end

            if length(stack) - l > 0 && time_to_rebuild(settings, rws, stack)
                rws, (i, j) =
                    reduce!(settings.algorithm, rws, stack, i, j, work)
                idxA = Automata.rebuild!(idxA, rws)
                @assert isempty(stack)
                # rws is reduced by now
            end
            if settings.verbosity == 1
                total = nrules(rws)
                stack_size = length(stack)
                settings.update_progress(total, i, stack_size)
            end
            j += 1
        end

        if i == lastindex(rwrules)
            if settings.verbosity == 2
                @info "reached end of rwrules with $(length(stack)) rules on stack"
            end
            rws, (i, _) = reduce!(settings.algorithm, rws, stack, i, 0, work)
            idxA = Automata.rebuild!(idxA, rws)
        end
        i += 1
    end

    return rws # so the rws is reduced here as well
end

function __post!(rws::AbstractRewritingSystem, rewriting, work::Workspace)
    settings = work.settings
    stack = Vector{Tuple{word_type(rws),word_type(rws)}}()

    if work.dropped_rules > 0
        @assert isempty(stack)
        if settings.verbosity ≥ 2
            @info "KnuthBendix completion has finished, however some rules were dropped; re-adding the defining rules"
        end
        for (lhs, rhs) in rws.rules_orig
            Words.store!(work.rewrite1, lhs)
            l = rewrite!(work.rewrite1, rewriting)
            Words.store!(work.rewrite2, rhs)
            r = rewrite!(work.rewrite2, rewriting)
            if l ≠ r
                push!(stack, (l, r))
            end
        end
        if !isempty(stack)
            if settings.verbosity ≥ 1
                @warn "The rws does NOT represent the original congruence. Re-adding the missing rules."
            end
            rws, _ = reduce!(settings.algorithm, rws, stack, 0, 0, work)
        else
            if settings.verbosity == 2
                @info "Some rules have been dropped but the congruence is preserved."
            end
        end
    end
    return rws
end

